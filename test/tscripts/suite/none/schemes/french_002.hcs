// none/schemes/french001.hcs   Test French Rep. scheme.

vocab frm {
    name "Month names";
    fieldname month;
    lang en;
    pseudo    Month,  Mon;
    tokens {
        1, "Vendémiaire",      "Vend";
        2, "Brumaire",         "Brum";
        3, "Frimaire",         "Frim";
        4, "Nivôse",           "Nivo";
        5, "Pluviôse",         "Pluv";
        6, "Ventôse",          "Vent";
        7, "Germinal",         "Germ";
        8, "Floréal",          "Flor";
        9, "Prairial",         "Prai";
        10, "Messidor",        "Mess";
        11, "Thermidor",       "Ther";
        12, "Fructidor",       "Fruc";
        13, "Complémentaires", "Comp";
    }
}

vocab frdd {
    name "Decade Day";
    fieldname dday;
    lang en;
    pseudo    DecDay,  DDay;
    tokens {
        1, "Primidi",  "Prim";
        2, "Duodi",    "Duo";
        3, "Tridi",    "Tri";
        4, "Quartidi", "Quar";
        5, "Quintidi", "Quin";
        6, "Sextidi",  "Sext";
        7, "Septidi",  "Sept";
        8, "Octidi",   "Oct";
        9, "Nonidi",   "Non";
        10, "Décadi",  "Dec";
    }
}

vocab frcomp {
    name "Complementary Days";
    fieldname CDay;
    lang en;
    pseudo    CompDay,           CDay;
    tokens {
        1, "Fête de la Vertu",      "Ver";
        2, "Fête du Génie",         "Gen";
        3, "Fête du Travail",       "Trav";
        4, "Fête de l'Opinion",     "Opin";
        5, "Fête des Récompenses",  "Rec";
        6, "Fête de la Révolution", "Rev";
    }
}

function if(c,a,b) { if c result=a; else result=b; endif }

grammar fr {
    fields year month day;
    optional dday;
    vocabs frm, frcomp, frdd;

    calculate CDay {
        output @if( month=13, day, ? ); // Output only if Complementary day.
        input { "", , 13, CDay };  // If comp day is entered, it must be month 13.
        pseudo CompDay;
    }
    calculate MDay {
        output @if( month<>13, day, ? ); // Output only if not a Complementary day
        alias day;
    }
    calculate NMonth {
        output @if( month<>13 or day=?, month, ? ); // Suppress month if it's the Complementary month unless it's all Complementary days
        alias month;       
    }

    format dcdmy "(dday:frdd:a) |(CDay:frcomp:a) |(MDay) |(NMonth:frm:a) |(year)";
    /*
    format dcdmy {
        input "(Day) |(Month) |(Year)";
        output "(DecDay:frdd) |(CompDay:frcomp) |(NMDay) |(NMonth:frm) |(Year)";
        rankout year, nmonth, nmday, cday, dday;
    }
    */
}

scheme fr {
    name "Test French Republican";
    base french;
    grammar fr;
}

writeln "";
writeln string,fr:dcdmy 2345678 +", "+ date,fr:dcdmy "Quar 4 Vent -82";
writeln string,fr:dcdmy 2432814 +", "+ date,fr:dcdmy "Trav 156";
writeln string,fr:dcdmy (2432812 ~ 2432817) +", "+ date,fr:dcdmy "Comp 156";
writeln string,fr:dcdmy (2432602 ~ 2432631) +", "+ date,fr:dcdmy "Vent 156";
writeln string,fr:dcdmy (2432452 ~ 2432817) +", "+ date,fr:dcdmy "156";

/*[OUTPUT]

Quar 4 Vent -82, 2345678
Trav 156, 2432814
Ver 156 ~ Rev 156, 2432812 ~ 2432817
Prim 1 Vent 156 ~ Dec 30 Vent 156, 2432602 ~ 2432631
Prim 1 Vend 156 ~ Rev 156, 2432452 ~ 2432817

[OUTPUT]*/
