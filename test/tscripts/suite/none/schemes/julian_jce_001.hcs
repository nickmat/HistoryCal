// none/schemes/julian_jce_001.hcs Original default version of jce

function if(c,a,b) { if c result=a; else result=b; endif }

vocab w {
    name "Weekday names";
    fieldname wday;
    lang en;
    pseudo    Weekday,  WDay;
    tokens {
        1, "Monday",    "Mon";
        2, "Tuesday",   "Tue";
        3, "Wednesday", "Wed";
        4, "Thursday",  "Thur";
        5, "Friday",    "Fri";
        6, "Saturday",  "Sat";
        7, "Sunday",    "Sun";
    }
}

vocab m {
    name "Month names";
    fieldname month;
    lang en;
    pseudo     Month,     Mon;
    tokens {
        1,  "January",   "Jan";
        2,  "February",  "Feb";
        3,  "March",     "Mar";
        4,  "April",     "Apr";
        5,  "May";
        6,  "June",      "Jun";
        7,  "July",      "Jul";
        8,  "August",    "Aug";
        9,  "September", "Sep";
        10, "October",   "Oct";
        11, "November",  "Nov";
        12, "December",  "Dec";
    }
}

vocab jce {
    name "Historic Era";
    fieldname ce;
    lang en;
    pseudo    CommonEra,         CE;
    tokens {
        0, "Before Common Era", "BCE";
        1, "Common Era",        "CE";
    }
}

grammar jce {
    fields year month day;
    optional wday, ce, ceyear;
    rank ce, ceyear, month, day;
    vocabs m, w, jce;
    calculate Era {
        output @if(year<500,:ce,?);
        pseudo "[CE<500]";
    }
    alias field {
        WDay,  wday;
        Day,   day;
        Month, month;
        Year,  ceyear;
        CEra,  ce;
        Era,   ce;
    }
    alias pseudo {
        w,    WDay;
        dd,   Day;
        mm,   Month;
        yyyy, Year;
    }
    alias unit {
        d, day;
        m, month;
        y, year;
        w, week;
    }
    format  dmy,    "(Day) |(Month:m:a) |(Year)| (Era:jce:a=CE)";
    /*
    format  dmye,    "(Day) |(Month:m:a) |(Year)| (CEra:jce:a=CE)";
    format  wdmy,   "(WDay:w:a) |(Day) |(Month:m:a) |(Year)| (Era:jce:a=CE)";
    format  out {
        output "(Day::os) |(Month:m) |(Year) |(CEra:jce:a)";
    }
    format  full {
        output "(WDay:w) |(Day::os) |(Month:m) |(Year) |(CEra:jce)";
    }
    format "dmy+",  "(Day) |(Month:m) |(Year) |(CEra:jce=CE)";
    format "wdmy+", "(WDay:w) |(Day) |(Month:m) |(Year) |(CEra:jce=CE)";
    format  mdy,    "(Month:m:a) |(Day), |(Year) |(CEra:jce:a=CE)";
    format  ymd,    "(Year)|(CEra:jce:a=CE)|:(Month:m:a)|:(Day)";
    */
    pref dmy;
}

scheme jce {
    name "Julian Common Era";
    base julian;
    grammar jce;
}

let d1 = date,jce:dmy "14sep948"; // 2067572  j:dmy# 14 Sep 948
let d2 = date,jce:dmy "20sep148ce"; // 1775378  j:dmy# 20 Sep 148
let d3 = date,jce:dmy "22sep149bce"; // 1667266  j:dmy# 22 Sep -148

writeln "";
writeln string,jce:dmy d1;
writeln string,jce:dmy d2;
writeln string,jce:dmy d3;

/*[OUTPUT]

14 Sep 948
20 Sep 148 CE
22 Sep 149 BCE

[OUTPUT]*/
