/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * Name:        scripts/ce-bce.hcs
 * Project:     Cal: Programmable Historical Calendar library.
 * Purpose:     Common Era and Before Common Era - default script.
 * Author:      Nick Matthews
 * Website:     http://historycal.org
 * Created:     17th June 2015
 * Copyright:   Copyright (c) 2015 ~ 2020, Nick Matthews.
 * Licence:     GNU GPLv3
 *
 *  The Cal library is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  The Cal library is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with the Cal library.  If not, see <http://www.gnu.org/licenses/>.
 *
 * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*/

lexicon jce {
    name "Historic Era";
    fieldname ce;
    lang en;
    pseudo    CommonEra,         CE;
    tokens {
        0, "Before Common Era", "BCE";
        1, "Common Era",        "CE";
    }
}

grammar jce {
    fields year month day;
    optional wday, ce, ceyear;
    rank ce, ceyear, month, day;
    lexicons m, w, jce;
    calculate Era {
        output @if(year<500,:ce,?);
        pseudo "[CE<500]";
    }
    alias field {
        WDay,  wday;
        Day,   day;
        Month, month;
        Year,  ceyear;
        CEra,  ce;
        Era,   ce;
    }
    alias pseudo {
        w,    WDay;
        dd,   Day;
        mm,   Month;
        yyyy, Year;
    }
    alias unit {
        d, day;
        m, month;
        y, year;
        w, week;
    }
    format  dmy,    "(Day) |(Month:m:a) |(Year)| (Era:jce:a=CE)";
    format  dmye,    "(Day) |(Month:m:a) |(Year)| (CEra:jce:a=CE)";
    format  wdmy,   "(WDay:w:a) |(Day) |(Month:m:a) |(Year)| (Era:jce:a=CE)";
    format  out {
        output "(Day::os) |(Month:m) |(Year) |(CEra:jce:a)";
    }
    format  full {
        output "(WDay:w) |(Day::os) |(Month:m) |(Year) |(CEra:jce)";
    }
    format "dmy+",  "(Day) |(Month:m) |(Year) |(CEra:jce=CE)";
    format "wdmy+", "(WDay:w) |(Day) |(Month:m) |(Year) |(CEra:jce=CE)";
    format  mdy,    "(Month:m:a) |(Day), |(Year) |(CEra:jce:a=CE)";
    format  ymd,    "(Year)|(CEra:jce:a=CE)|:(Month:m:a)|:(Day)";
    pref dmy;
}

scheme jce {
    name "Julian Common Era";
    base julian;
    grammar jce;
}

scheme gce {
    name "Gregorian Common Era";
    base gregorian;
    grammar jce;
}

// End of scripts/ce-bce.hcs file
