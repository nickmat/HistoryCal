/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * Name:        scripts/julian_hybrid.hcs
 * Project:     Cal: Programmable Historical Calendar library.
 * Purpose:     Default script.
 * Author:      Nick Matthews
 * Website:     http://historycal.org
 * Created:     30th September 2014
 * Copyright:   Copyright (c) 2014 ~ 2016, Nick Matthews.
 * Licence:     GNU GPLv3
 *
 *  The Cal library is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  The Cal library is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with the Cal library.  If not, see <http://www.gnu.org/licenses/>.
 *
 * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*/

vocab jg {
    name "Scheme names";
    fieldname scheme;
    lang en;
    pseudo    Scheme, Sch;
    tokens {
        0, "Julian",     "j";
        1, "Gregorian",  "g";
    }
}

grammar jg {
    inherit j;
    vocabs jg;
    alias field {
        Scheme, scheme;
    }
    alias pseudo {
        s, Scheme;
    }
    format  dmys,   "(Day) |(Month:m.a) |(Year) |(Scheme:jg.a)";
    format "dmys+", "(Day) |(Month:m) |(Year) |(Scheme:jg)";
    pref dmy;
}

scheme ay {
    name "Astronomical Year";
    hybrid {
        fields year, month, day;
        scheme j;
        change 2299161;  // g# 15 Oct 1582
        scheme g;
    }
    grammar jg;
}

vocab jajg {
    name "Scheme names";
    fieldname scheme;
    lang en;
    pseudo    Scheme,   Sch;
    tokens {
        0, "Julian Ann", "ja";
        1, "Julian",     "j";
        2, "Gregorian",  "g";
    }
}

grammar hy {
    optional unshift, wday;
    vocabs w, m, jajg;
    alias field {
        WDay,        wday;
        Day,         day;
        Month,       month;
        Year,        year;
        "Hist-Year", unshift;
        Scheme,      scheme;
    }
    alias pseudo {
        w,    WDay;
        dd,   Day;
        mm,   Month;
        yyyy, Year;
        y,    "Hist-Year";
        s,    Scheme;
    }
    format dmyh,  "(Day) |(Month:m.a) |(Year/Hist-Year)";
    format dmy,   "(Day) |(Month:m.a) |(Year)";
    format dmys,  "(Day) |(Month:m.a) |(Year) |(Scheme:jajg.a)";
    format wdmyh, "(WDay:w.a) |(Day) |(Month:m.a) |(Year/Hist-Year)";
    format wdmyhs,"(WDay:w.a) |(Day) |(Month:m.a) |(Year/Hist-Year) |(Scheme:jajg)";
    pref dmyh;
}

scheme eng {
    name "English Hybrid";
    hybrid {
        fields year, month, day;
        scheme ja;
        change 2360976;  // j# 1 Jan 1752
        scheme j;
        change 2361222;  // g# 14 Sep 1752
        scheme g;
    }
    grammar hy;
}

scheme scot {
    name "Scottish Hybrid";
    hybrid {
        fields year, month, day;
        scheme ja;
        change 2305458;  // j# 1 Jan 1600
        scheme j;
        change 2361222;  // g# 14 Sep 1752
        scheme g;
    }
    grammar hy;
}

// End of scripts/julian_hybrid.hcs file

